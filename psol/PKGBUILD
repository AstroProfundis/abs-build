# vim:set ts=2 sw=2 et ft=sh tw=100: expandtab
# Maintainer: Piotr Rogoża <rogoza dot piotr at gmail dot com>
# Contributor: Piotr Rogoża <rogoza dot piotr at gmail dot com>

pkgname=psol
pkgver=1.8.31.3
pkgrel=1
pkgdesc='PageSpeed Optimization Library'
arch=('i686' 'x86_64')
url='https://developers.google.com/speed/pagespeed/psol'
license=('Apache')
options=(!strip)
source=("$pkgname-$pkgver.tar.gz::https://dl.google.com/dl/page-speed/psol/${pkgver}.tar.gz")
md5sums=('631b58a544c451ba8243235754191b34')

package(){
  cd "$srcdir"/$pkgname
  install -dm755 "$pkgdir"/usr/lib/psol
  tar -c ./ --exclude include_history.txt \
    | tar -x -C "$pkgdir"/usr/lib/psol

  find "$pkgdir"/usr/lib/psol -type d -exec chmod 0755 '{}' ';'
  find "$pkgdir"/usr/lib/psol -type f -exec chmod 0644 '{}' ';'
  case $CARCH in
    i686)
      rm -rf "$pkgdir"/usr/lib/psol/lib/{Debug/linux/x64,Release/linux/x64}
      ;;
    x86_64)
      rm -rf "$pkgdir"/usr/lib/psol/lib/{Debug/linux/ia32,Release/linux/ia32}
      ;;
  esac
}

